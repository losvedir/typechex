WHITESPACE = _{ " " | "\n" | "\r" | "\t" }

file = { SOI ~ expr ~ EOI }
// defmodule = { "{" ~ ":defmodule" ~ "," ~ "[" }

expr = _{ integer | float | atom | binary | list | tuple }


// numbers
integer = { ASCII_DIGIT+ }
float = { ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }


// binaries (aka strings)
binary = ${ "\"" ~ binary_inner ~ "\"" }
binary_inner = @{ non_quote_char* }


// atoms
atom = ${ atom_literal | ":" ~ atom_identifier }
atom_literal = { "true" | "false" | "nil" | "Kernel" | "Access" }
atom_identifier = { atom_undocumented | atom_unquoted | atom_quoted }
atom_undocumented = {
    "<>"
    | "<<>>"
    | "{}"
    | "++"
    | "||"
    | "&&"
    | "=="
    | "--"
    | "&&"
    | "\\\\"
    | "%{}"
    | "!"
    | "@"
    | "%"
    | "^"
    | "&"
    | "*"
    | "+"
    | "="
    | "-"
    | "/"
    | "|"
    | "<"
    | ">"
    | "."
}
atom_unquoted = ${ (LETTER | "_") ~ (LETTER | NUMBER | "_" | "@")* ~ ("!" | "?")? }
atom_quoted = ${ "\"" ~ non_quote_char* ~ "\"" }


// lists
list = { "[" ~ ((expr | sugar_tuple) ~ ",")* ~ (expr | sugar_tuple)? ~ "]" }
sugar_tuple = { backwards_atom ~ expr }
backwards_atom = @{ atom_identifier ~ ":" }


// tuples
tuple = { "{" ~ (expr ~ ",")* ~ expr? ~ "}" }


// helpers
non_quote_char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ "\""
}
